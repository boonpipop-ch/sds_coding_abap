FUNCTION Z_SDSFI_GET_CUSTOMER_CREDIT.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     VALUE(IF_PARTNER) TYPE  BU_PARTNER
*"  EXPORTING
*"     VALUE(ES_CUSTOMER_CREDIT) TYPE  ZSDSFIS129
*"     VALUE(ES_CREDIT_SUMMARY) TYPE  ZSDSFIS140
*"     VALUE(ET_PROJECT_CREDIT) TYPE  ZSDSFIS120_TT
*"----------------------------------------------------------------------
*-----------------------------------------------------------------------
*  Program ID         : ZSDSFIR0320_F01
*  Creation Date      : 05.06.2024
*  Author             : Thanapong C. (Eviden)
*  Add-on ID          : FSCMR013
*  Description        : This is the RFC Function to get the customer credit
*                       and project credit
*  Purpose            : RFC FM is calling from SF to get the customer credit
*                       and project credit
*  Copied from        : N/A
*  Restriction        : N/A
*-----------------------------------------------------------------------
*  CHANGE HISTORY
*-----------------------------------------------------------------------
*  Date        Task #      Programmer  Description
*-----------------------------------------------------------------------
*  DD.MM.YYYY  TR no.      ABAP Name   Detail
*-----------------------------------------------------------------------

  CONSTANTS:
    LC_CREDIT_SGMNT_SDS TYPE UKM_CREDIT_SGMNT VALUE '1000'.

  DATA:
    LF_TEXT          TYPE STRING,
    LF_PARTNER       TYPE BU_PARTNER,
    LT_PARTNER_RANGE TYPE BUP_PARTNER_RANGE_T.

  DATA(LS_DOCTYPE) =  ZCL_SDSFI_CUSTOMER_CREDIT=>GC_DOC_TYPE.

  CLEAR: ES_CUSTOMER_CREDIT,
         ES_CREDIT_SUMMARY,
         ET_PROJECT_CREDIT.

  LF_PARTNER = |{ IF_PARTNER ALPHA = IN }|.

  ES_CUSTOMER_CREDIT-PARTNER = IF_PARTNER.

  ZCL_SDSFI_CUSTOMER_CREDIT=>VALIDATE_PARTNER(
    EXPORTING
      IF_PARTNER = LF_PARTNER
    IMPORTING
      EF_INVALID = DATA(LF_INVALID)
      EF_MSGTX   = LF_TEXT
  ).
  IF LF_INVALID EQ ABAP_TRUE.
    ES_CUSTOMER_CREDIT-RESP_STATUS  = ZCL_SDSCA_REST_INTF_UTILITY=>GC_ERROR.
    ES_CUSTOMER_CREDIT-RESP_MESSAGE = LF_TEXT.
    RETURN.
  ENDIF.

  APPEND VALUE #( SIGN   = 'I'
                  OPTION = 'EQ'
                  LOW    = LF_PARTNER )
            TO LT_PARTNER_RANGE.

  ZCL_SDSFI_CUSTOMER_CREDIT=>GET_DATA(
    EXPORTING
      IT_PARTNER      = LT_PARTNER_RANGE     " Table Type EDM_PARTN_RANGE
      IF_CREDIT_SGMNT = LC_CREDIT_SGMNT_SDS  " Credit Segments
      IF_ALL_CUSTOMER = ABAP_TRUE            " All Customer
      IF_SEL_CREDIT   = '*'                  " Selection (C: Customer Credit, P: Project Credit, *: All )
    IMPORTING
      ET_RESULT       = DATA(LT_RESULT)
  ).

  IF LT_RESULT IS INITIAL.
*     Message: No data found.
    MESSAGE S001(ZSDSCA01) INTO LF_TEXT.
    ES_CUSTOMER_CREDIT-RESP_STATUS = ZCL_SDSCA_REST_INTF_UTILITY=>GC_ERROR.
    ES_CUSTOMER_CREDIT-RESP_STATUS = LF_TEXT.
    RETURN.
  ENDIF.

  READ TABLE LT_RESULT ASSIGNING FIELD-SYMBOL(<L_RESULT>) INDEX 1.
  IF SY-SUBRC IS INITIAL.

    ES_CUSTOMER_CREDIT-PARTNER      = IF_PARTNER.
    ES_CUSTOMER_CREDIT-PARTNER_NAME = <L_RESULT>-PARTNER_NAME.
    ES_CUSTOMER_CREDIT-CLERK        = <L_RESULT>-ACCT_CLERK_TX.

    ES_CREDIT_SUMMARY-CREDIT_EXPOSURE      = <L_RESULT>-CREDIT_EXPOSURE.
    ES_CREDIT_SUMMARY-CREDIT_LIMIT         = <L_RESULT>-CREDIT_LIMIT.
    ES_CREDIT_SUMMARY-REMAIN_CREDIT_LIMIT  = <L_RESULT>-REMAIN_CREDIT_LIMIT.
    ES_CREDIT_SUMMARY-UTILIZATION          = <L_RESULT>-UTILIZATION_PERCENT.

    LOOP AT <L_RESULT>-SUMMARY_DOC_TYPE ASSIGNING FIELD-SYMBOL(<L_SUMMARY_DOC_TYPE>)
    WHERE PSPID IS INITIAL.

      CASE <L_SUMMARY_DOC_TYPE>-DOC_TYPE.
        WHEN LS_DOCTYPE-SALEORDER.
          ES_CUSTOMER_CREDIT-SUMMARY_SO     = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-BILL.
          ES_CUSTOMER_CREDIT-SUMMARY_BILLING = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-AR_INVOICE.
          ES_CUSTOMER_CREDIT-SUMMARY_AR_INVOICE = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-DOWN_PATMENT.
          ES_CUSTOMER_CREDIT-SUMMARY_DOWN_PAYMENT = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-ADV_RECEIVE.
          ES_CUSTOMER_CREDIT-SUMMARY_ADVANCE_RECEIVED = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-POST_DATE_CHEQUE.
          ES_CUSTOMER_CREDIT-SUMMARY_POSTED_DATE_CHEQUE = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-CHQ_RETURN.
          ES_CUSTOMER_CREDIT-SUMMARY_CHQ_RETURN = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-RETENTION.
          ES_CUSTOMER_CREDIT-SUMMARY_RETENTION = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-AVAL.
          ES_CUSTOMER_CREDIT-SUMMARY_AVAL = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-DLC.
          ES_CUSTOMER_CREDIT-SUMMARY_DLC  = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-BANK_GUARANTEE.
          ES_CUSTOMER_CREDIT-SUMMARY_BANK_GUARANTEE = <L_SUMMARY_DOC_TYPE>-AMOUNT.

        WHEN LS_DOCTYPE-CREDIT_INSURANCE.
          ES_CUSTOMER_CREDIT-SUMMARY_CREDIT_INSURANCE = <L_SUMMARY_DOC_TYPE>-AMOUNT.

      ENDCASE.

    ENDLOOP.

    LOOP AT <L_RESULT>-SUMMARY_PROJ ASSIGNING FIELD-SYMBOL(<L_SUMMARY_PROJ>).

      " Customer Credit
      IF <L_SUMMARY_PROJ>-PSPID IS INITIAL.
        ES_CUSTOMER_CREDIT-CREDIT_SGMNT        = <L_SUMMARY_PROJ>-CREDIT_SGMNT.
        ES_CUSTOMER_CREDIT-XBLOCKED            = <L_SUMMARY_PROJ>-XBLOCKED.
        ES_CUSTOMER_CREDIT-CREDIT_LIMIT        = <L_SUMMARY_PROJ>-CREDIT_LIMIT.
        ES_CUSTOMER_CREDIT-CREDIT_EXPOSURE     = <L_SUMMARY_PROJ>-CREDIT_EXPOSURE.
        ES_CUSTOMER_CREDIT-UTILIZATION         = <L_SUMMARY_PROJ>-UTILIZATION_PERCENT.
        ES_CUSTOMER_CREDIT-RISK_CLASS          = <L_SUMMARY_PROJ>-RISK_CLASS_TXT.
        ES_CUSTOMER_CREDIT-REMAIN_CREDIT_LIMIT = <L_SUMMARY_PROJ>-REMAIN_CREDIT_LIMIT.
        ES_CUSTOMER_CREDIT-PAYMENT_TERM        = <L_SUMMARY_PROJ>-ZTERM.
      ELSE. " Project Credit
        APPEND INITIAL LINE TO ET_PROJECT_CREDIT ASSIGNING FIELD-SYMBOL(<L_PROJECT_CREDIT>).
        <L_PROJECT_CREDIT>-PROJECT_DEF           = <L_SUMMARY_PROJ>-PSPID.
        <L_PROJECT_CREDIT>-PROJECT_SUMMARY       = <L_SUMMARY_PROJ>-CREDIT_LIMIT.
        <L_PROJECT_CREDIT>-UTILIZATION           = <L_SUMMARY_PROJ>-UTILIZATION_PERCENT.
        <L_PROJECT_CREDIT>-CREDIT_EXPOSURE       = <L_SUMMARY_PROJ>-CREDIT_EXPOSURE.
        <L_PROJECT_CREDIT>-REMAIN_CREDIT_LIMIT   = <L_SUMMARY_PROJ>-REMAIN_CREDIT_LIMIT.
        <L_PROJECT_CREDIT>-PAYMENT_TERM          = <L_SUMMARY_PROJ>-ZTERM.
        <L_PROJECT_CREDIT>-PROJECT_DESCRIPTION   = <L_SUMMARY_PROJ>-PROJECT_TX.
      ENDIF.

      ES_CUSTOMER_CREDIT-RESP_STATUS = ZCL_SDSCA_REST_INTF_UTILITY=>GC_SUCCESS.

      " Text-s01: Success: Retrieved credit data for business partner
      LF_TEXT = TEXT-S01.
      ES_CUSTOMER_CREDIT-RESP_MESSAGE = LF_TEXT.

    ENDLOOP.

  ENDIF.

ENDFUNCTION.
