*----------------------------------------------------------------------*
***INCLUDE LZSDSFI04F01.
*----------------------------------------------------------------------*
*&---------------------------------------------------------------------*
*& Form F_CHANGE_ASSET
*&---------------------------------------------------------------------*
*& text
*&---------------------------------------------------------------------*
*&      --> LS_INPUT
*&      <-- E_MESTYPE
*&      <-- E_MESSAGE
*&---------------------------------------------------------------------*
FORM F_CHANGE_ASSET  USING    LS_INPUT   TYPE ZSDSFIS087
                     CHANGING LT_MESSAGE TYPE ZSDSFIS088_TT.
  DATA : LS_TIMEDEPENDENTDATA	 TYPE BAPI1022_FEGLG003,
         LS_TIMEDEPENDENTDATAX TYPE BAPI1022_FEGLG003X.

  DATA : LS_RETURN TYPE BAPIRET2.

  DATA : LS_MESSAGE TYPE ZSDSFIS088.

  CONSTANTS : BEGIN OF LC_CON,
                AE   TYPE C LENGTH 2 VALUE 'AE',
                S    TYPE C LENGTH 1 VALUE 'S',
                E    TYPE C LENGTH 1 VALUE 'E',
                1000 TYPE C LENGTH 4 VALUE '1000',
                TOD  TYPE SY-DATUM VALUE '99991231',
              END OF LC_CON.

  LS_TIMEDEPENDENTDATA-FROM_DATE = SY-DATUM.
  LS_TIMEDEPENDENTDATA-TO_DATE   = LC_CON-TOD.

  LS_TIMEDEPENDENTDATAX-FROM_DATE = ABAP_TRUE.
  LS_TIMEDEPENDENTDATAX-TO_DATE   = ABAP_TRUE.

  LS_TIMEDEPENDENTDATA-COSTCENTER  = |{ LS_INPUT-OWNER_NEW_COSTCENTER ALPHA = OUT }| .
  LS_TIMEDEPENDENTDATA-ROOM        = |{ LS_INPUT-OWNER_NEW ALPHA = IN }| .
  LS_TIMEDEPENDENTDATA-LOCATION    = LS_INPUT-OWNER_NEW_LOCATION.
  LS_TIMEDEPENDENTDATA-PERSON_NO   = |{ LS_INPUT-OWNER_NEW_MANAGER ALPHA = OUT }| .

  IF LS_TIMEDEPENDENTDATA-COSTCENTER IS NOT INITIAL.
    LS_TIMEDEPENDENTDATAX-COSTCENTER = ABAP_TRUE.
  ENDIF.

  IF LS_TIMEDEPENDENTDATA-PERSON_NO  IS NOT INITIAL.
    LS_TIMEDEPENDENTDATAX-PERSON_NO  = ABAP_TRUE.
  ENDIF.

  IF LS_TIMEDEPENDENTDATA-LOCATION IS NOT INITIAL.
    LS_TIMEDEPENDENTDATA-PLANT       = LC_CON-1000.
    LS_TIMEDEPENDENTDATAX-PLANT      = ABAP_TRUE.
    LS_TIMEDEPENDENTDATAX-LOCATION   = ABAP_TRUE.
  ENDIF.

  IF LS_TIMEDEPENDENTDATA-ROOM  IS NOT INITIAL.
    LS_TIMEDEPENDENTDATAX-ROOM = ABAP_TRUE.
  ENDIF.

  CALL FUNCTION 'BAPI_FIXEDASSET_CHANGE'
    EXPORTING
      COMPANYCODE        = LS_INPUT-COMCODE
      ASSET              = LS_INPUT-FIX_ASSET_NO
      SUBNUMBER          = LS_INPUT-FIX_ASSET_SUB_NO
      TIMEDEPENDENTDATA  = LS_TIMEDEPENDENTDATA
      TIMEDEPENDENTDATAX = LS_TIMEDEPENDENTDATAX
    IMPORTING
      RETURN             = LS_RETURN.

  LS_MESSAGE-FIX_ASSET_NO     = LS_INPUT-FIX_ASSET_NO.
  LS_MESSAGE-FIX_ASSET_SUB_NO = LS_INPUT-FIX_ASSET_SUB_NO.

  IF LS_RETURN-TYPE CA LC_CON-AE.
    LS_MESSAGE-MESSAGE_TYPE  = LC_CON-E.
    LS_MESSAGE-MESSAGE       = LS_RETURN-MESSAGE.
  ELSE.
    LS_MESSAGE-MESSAGE_TYPE  = LC_CON-S.
    CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
      EXPORTING
        WAIT = ABAP_TRUE.
  ENDIF.

  APPEND LS_MESSAGE TO LT_MESSAGE.
ENDFORM.
