FUNCTION Z_SDSFI_AL11_FILELIST.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(IM_DIRECTORY)
*"  EXPORTING
*"     REFERENCE(EX_AL11_FILES) TYPE  ZSDSFIS024_TT
*"     REFERENCE(EX_AL11_FILELIST) TYPE  ZSDSFIS070_TT
*"  EXCEPTIONS
*"      NO_FILE_FOUND
*"----------------------------------------------------------------------
  TYPES: BEGIN OF LTY_FNAME,
           PREFIX      TYPE C LENGTH 05,
           YYMMDD      TYPE C LENGTH 06,
           HHMMSS      TYPE T,
           COMPANYCODE TYPE BUKRS,
           SAPOBJECTID TYPE SAEOBJID,
           OUTPUTTYPE  TYPE KSCHL,
         END OF LTY_FNAME.

  DATA: LIT_LIST     TYPE ZSDSFIS070_TT,
        LIT_FILEOUT  TYPE ZSDSFIS024_TT,
        LIT_FILE     TYPE TTY_FILE,
        LIT_DIR_LIST TYPE TABLE OF EPSFILI.

  DATA: LV_FILE      TYPE STRING,
        LV_YYYYMMDD  TYPE D,
        LV_LSCOMMAND TYPE STRING,
        LV_FULL      TYPE STRING,
        LV_DIRNAME   TYPE EPSF-EPSDIRNAM,
        LV_TMP       TYPE CHAR10.
  DATA: LWA_FNAME    TYPE LTY_FNAME,
        LWA_LIST     LIKE LINE OF LIT_LIST,
        LWA_FILEOUT  LIKE LINE OF LIT_FILEOUT,
        LWA_FILE     LIKE LINE OF LIT_FILE,
        LWA_DIR_LIST LIKE LINE OF LIT_DIR_LIST.

  FIELD-SYMBOLS: <LFS_LIST> LIKE LINE OF LIT_LIST.


* Function SUBST_GET_FILE_LIST can be used but in some SAP version
* this function does not exist.
  PERFORM ZFILL_FILE_LIST USING IM_DIRECTORY
                       CHANGING LIT_FILE.

  LOOP AT LIT_FILE INTO LWA_FILE.

    SPLIT LWA_FILE-NAME AT '.' INTO LV_FILE LV_TMP.

    CLEAR LWA_FNAME.
    SPLIT LV_FILE AT '_'
     INTO LWA_FNAME-PREFIX
          LWA_FNAME-YYMMDD
          LWA_FNAME-HHMMSS
*          lwa_fname-companycode
*          lwa_fname-sapobjectid
          LWA_FNAME-SAPOBJECTID
          LWA_FNAME-COMPANYCODE
          LWA_FNAME-OUTPUTTYPE.

    CONCATENATE '20' LWA_FNAME-YYMMDD
           INTO LV_YYYYMMDD.
    CONCATENATE LWA_FILE-DIRNAME LWA_FILE-NAME
           INTO LV_FULL.

    CLEAR LWA_LIST.
    LWA_LIST-SAPOBJECTID   = LWA_FNAME-SAPOBJECTID.
    LWA_LIST-COMPANYCODE   = LWA_FNAME-COMPANYCODE.
    LWA_LIST-OUTPUTTYPE    = LWA_FNAME-OUTPUTTYPE.
    LWA_LIST-YYYYMMDD      = LV_YYYYMMDD.
    LWA_LIST-HHMMSS        = LWA_FNAME-HHMMSS.
    LWA_LIST-FILENAME      = LWA_FILE-NAME.
    LWA_LIST-FULLFILENAME  = LV_FULL.
    APPEND LWA_LIST TO LIT_LIST.

  ENDLOOP.  "lit_file

  SORT LIT_LIST BY SAPOBJECTID ASCENDING
                   COMPANYCODE ASCENDING
                   OUTPUTTYPE  ASCENDING
                   YYYYMMDD    DESCENDING
                   HHMMSS      DESCENDING.

  LOOP AT LIT_LIST ASSIGNING <LFS_LIST>.
    AT NEW OUTPUTTYPE.
      CLEAR LWA_FILEOUT.
      LWA_FILEOUT-WA_LATEST = <LFS_LIST>.
    ENDAT.

    APPEND <LFS_LIST> TO LWA_FILEOUT-IT_ALL.

    AT END OF OUTPUTTYPE.
      APPEND LWA_FILEOUT TO LIT_FILEOUT.
    ENDAT.
  ENDLOOP.  "lit_list

  SORT LIT_FILEOUT BY WA_LATEST-SAPOBJECTID
                      WA_LATEST-COMPANYCODE
                      WA_LATEST-OUTPUTTYPE.

  EX_AL11_FILES[]    = LIT_FILEOUT[].
  EX_AL11_FILELIST[] = LIT_LIST[].

  IF EX_AL11_FILES[] IS INITIAL.
    RAISE NO_FILE_FOUND.
  ENDIF.



ENDFUNCTION.
