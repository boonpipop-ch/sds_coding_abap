FUNCTION Z_SDSFI_ASSET_CREATE.
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     REFERENCE(IS_INPUT) TYPE  ZSDSFIS011
*"  EXPORTING
*"     REFERENCE(ET_RETURN) TYPE  ZSDSFIS012_TT
*"     REFERENCE(E_MESSAGE) TYPE  CHAR255
*"     REFERENCE(E_MESTYPE) TYPE  CHAR1
*"----------------------------------------------------------------------

  DATA: LS_KEY       TYPE BAPI1022_KEY,
        LS_REFERENCE TYPE BAPI1022_REFERENCE.

  DATA: LS_RETURN     TYPE BAPIRET2.

  DATA: LS_GENERALDATA  TYPE BAPI1022_FEGLG001,
        LS_GENERALDATAX TYPE BAPI1022_FEGLG001X.

  DATA: LS_TIMEDEPENDENTDATA  TYPE BAPI1022_FEGLG003,
        LS_TIMEDEPENDENTDATAX TYPE BAPI1022_FEGLG003X.

  DATA: LS_INVESTACCTASSIGNMNT  TYPE BAPI1022_FEGLG010,
        LS_INVESTACCTASSIGNMNTX TYPE BAPI1022_FEGLG010X.

  DATA: LS_RETURN_DATA LIKE LINE OF ET_RETURN.

  DATA: LV_LOOP TYPE I.

  DATA: LCL_UTIL TYPE REF TO ZCL_SDSCA_UTIL_SDS.

  CONSTANTS : BEGIN OF LC_CON,
                AE TYPE C LENGTH 2 VALUE 'AE',
                S  TYPE C LENGTH 1 VALUE 'S',
                E  TYPE C LENGTH 1 VALUE 'E',
              END OF LC_CON.

  IF LCL_UTIL IS NOT BOUND.
    CREATE OBJECT LCL_UTIL.
  ENDIF.

  IF IS_INPUT-QTY EQ 0.
    LV_LOOP = 1.
  ELSE.
    LV_LOOP = IS_INPUT-QTY.
  ENDIF.

  DO LV_LOOP TIMES.

    LS_KEY-COMPANYCODE        = IS_INPUT-COMPANY_CODE.
*  LS_REFERENCE-COMPANYCODE  = IS_INPUT-COMPANY_CODE.

    LS_GENERALDATA-ASSETCLASS = IS_INPUT-ASSET_CLASS.
    LS_GENERALDATA-DESCRIPT   = IS_INPUT-DESCRIPTION1.
    LS_GENERALDATA-DESCRIPT2  = IS_INPUT-DESCRIPTION2.

    LS_GENERALDATAX-ASSETCLASS = ABAP_TRUE.
    LS_GENERALDATAX-DESCRIPT   = ABAP_TRUE.
    LS_GENERALDATAX-DESCRIPT2  = ABAP_TRUE.

    LS_TIMEDEPENDENTDATA-COSTCENTER  = |{ IS_INPUT-COST_CENTER ALPHA = IN }|.
    LS_TIMEDEPENDENTDATAX-COSTCENTER = ABAP_TRUE.

    LS_TIMEDEPENDENTDATA-ROOM = LCL_UTIL->GET_EMPCODE_FROM_COSTCENTER( LS_TIMEDEPENDENTDATA-COSTCENTER ).
    IF LS_TIMEDEPENDENTDATA-ROOM IS NOT INITIAL.
      LS_TIMEDEPENDENTDATAX-ROOM = ABAP_TRUE.
    ENDIF.

    LS_TIMEDEPENDENTDATA-PERSON_NO   = |{ IS_INPUT-OWNER ALPHA = IN }|.
    LS_TIMEDEPENDENTDATAX-PERSON_NO  = ABAP_TRUE.

    LS_INVESTACCTASSIGNMNT-WBS_ELEMENT  = IS_INPUT-WBSNO.
    LS_INVESTACCTASSIGNMNTX-WBS_ELEMENT = ABAP_TRUE.

    CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
      EXPORTING
        INPUT  = LS_TIMEDEPENDENTDATA-COSTCENTER
      IMPORTING
        OUTPUT = LS_TIMEDEPENDENTDATA-COSTCENTER.

    LS_TIMEDEPENDENTDATAx-COSTCENTER = ABAP_TRUE.

    CALL FUNCTION 'BAPI_FIXEDASSET_CREATE1'
      EXPORTING
        KEY                  = LS_KEY
*       REFERENCE            = LS_REFERENCE
        GENERALDATA          = LS_GENERALDATA
        GENERALDATAX         = LS_GENERALDATAX
        TIMEDEPENDENTDATA    = LS_TIMEDEPENDENTDATA
        TIMEDEPENDENTDATAX   = LS_TIMEDEPENDENTDATAX
        INVESTACCTASSIGNMNT  = LS_INVESTACCTASSIGNMNT
        INVESTACCTASSIGNMNTX = LS_INVESTACCTASSIGNMNTX
      IMPORTING
        RETURN               = LS_RETURN.

    IF LS_RETURN-TYPE CA LC_CON-AE.
      E_MESTYPE  = LC_CON-E.
      E_MESSAGE  = LS_RETURN-MESSAGE.
      EXIT.
    ELSE.
      E_MESTYPE               = LC_CON-S.
      LS_RETURN_DATA-ANLKL    = IS_INPUT-ASSET_CLASS.
      LS_RETURN_DATA-BUKRS    = LS_RETURN-MESSAGE_V3.
      LS_RETURN_DATA-ANLN1    = LS_RETURN-MESSAGE_V1.
      LS_RETURN_DATA-ANLN2    = LS_RETURN-MESSAGE_V2.
      LS_RETURN_DATA-MATGROUP = IS_INPUT-MATGROUP.
      APPEND LS_RETURN_DATA TO ET_RETURN.

      CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
        EXPORTING
          WAIT = ABAP_TRUE.

    ENDIF.
    CLEAR : LS_KEY,LS_GENERALDATA,LS_GENERALDATAX,
            LS_TIMEDEPENDENTDATA,LS_TIMEDEPENDENTDATAX,LS_RETURN.
  ENDDO.
ENDFUNCTION.
