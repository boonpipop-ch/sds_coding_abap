FUNCTION Z_SDSFI_BUDGET_STATUS .
*"----------------------------------------------------------------------
*"*"Local Interface:
*"  IMPORTING
*"     VALUE(IV_BUDGET_ID) TYPE  ZSDSDE_BUDGET_ID OPTIONAL
*"  EXPORTING
*"     VALUE(ET_RETURN) TYPE  BAPIRET2_T
*"----------------------------------------------------------------------

  DATA: LV_BUDGET_ID TYPE ZSDSDE_BUDGET_ID,
        LV_OBJECT_ID TYPE JEST-OBJNR,
        LV_AUFNR     TYPE AUFK-AUFNR,
        LV_POSID     TYPE PRPS-POSID.

  LV_BUDGET_ID = IV_BUDGET_ID.

  CONDENSE LV_BUDGET_ID.
  IF LV_BUDGET_ID IS INITIAL.
    APPEND INITIAL LINE TO ET_RETURN ASSIGNING FIELD-SYMBOL(<LFS_RETURN>).
    <LFS_RETURN>-TYPE       = GC_MSG_TYPE-ERROR.
    <LFS_RETURN>-MESSAGE    = TEXT-E01.
    RETURN.
  ENDIF.

  MOVE: LV_BUDGET_ID TO LV_AUFNR,
        LV_BUDGET_ID TO LV_POSID.

  CALL FUNCTION 'CONVERSION_EXIT_ALPHA_INPUT'
    EXPORTING
      INPUT  = LV_AUFNR
    IMPORTING
      OUTPUT = LV_AUFNR.

  CALL FUNCTION 'CONVERSION_EXIT_ABPSN_INPUT'
    EXPORTING
      INPUT  = LV_POSID
    IMPORTING
      OUTPUT = LV_POSID.

  SELECT SINGLE * FROM PRPS   INTO @DATA(LS_PRPS)
                              WHERE POSID = @LV_POSID.
  IF SY-SUBRC = 0.
    LV_OBJECT_ID = LS_PRPS-OBJNR.
  ELSE.
    SELECT SINGLE * FROM AUFK INTO @DATA(LS_AUFK)
                              WHERE AUFNR = @LV_AUFNR.
    IF SY-SUBRC = 0.
      LV_OBJECT_ID = LS_AUFK-OBJNR.
    ENDIF.
  ENDIF.

  IF LS_PRPS IS INITIAL AND
     LS_AUFK IS INITIAL.
    APPEND INITIAL LINE TO ET_RETURN ASSIGNING <LFS_RETURN>.
    <LFS_RETURN>-TYPE       = GC_MSG_TYPE-ERROR.
    <LFS_RETURN>-MESSAGE    = TEXT-E02.
    RETURN.
  ENDIF.

  SELECT JEST~OBJNR,
         JEST~STAT,
         JEST~CHGNR,
         TJ02T~SPRAS,
         TJ02T~TXT04,
         TJ02T~TXT30
    INTO TABLE @DATA(LT_JEST) FROM JEST
                              INNER JOIN TJ02T ON TJ02T~ISTAT = JEST~STAT
                                                WHERE OBJNR = @LV_OBJECT_ID
                                                 AND INACT = @SPACE
                                                 AND SPRAS = @SY-LANGU.
  IF SY-SUBRC = 0.
    LOOP AT LT_JEST INTO DATA(LS_JEST) WHERE STAT = 'I0046'.
      EXIT.
    ENDLOOP.
    IF SY-SUBRC = 0.
      "able to get status
      APPEND INITIAL LINE TO ET_RETURN ASSIGNING <LFS_RETURN>.
      <LFS_RETURN>-TYPE       = GC_MSG_TYPE-ERROR.
      <LFS_RETURN>-MESSAGE    = TEXT-E04.
      <LFS_RETURN>-MESSAGE_V1 = LS_JEST-TXT04.
      <LFS_RETURN>-MESSAGE_V2 = LS_JEST-TXT30.
    ELSE.
      "able to get status
      SORT LT_JEST BY CHGNR DESCENDING.
      READ TABLE LT_JEST INTO LS_JEST INDEX 1.
      IF SY-SUBRC = 0.
        APPEND INITIAL LINE TO ET_RETURN ASSIGNING <LFS_RETURN>.
        <LFS_RETURN>-TYPE       = GC_MSG_TYPE-SUCESS.
        <LFS_RETURN>-MESSAGE    = TEXT-S01.
        <LFS_RETURN>-MESSAGE_V1 = LS_JEST-TXT04.
        <LFS_RETURN>-MESSAGE_V2 = LS_JEST-TXT30.
        RETURN.
      ENDIF.
    ENDIF.
  ELSE.
    APPEND INITIAL LINE TO ET_RETURN ASSIGNING <LFS_RETURN>.
    <LFS_RETURN>-TYPE       = GC_MSG_TYPE-ERROR.
    <LFS_RETURN>-MESSAGE    = TEXT-E03.
    RETURN.
  ENDIF.

ENDFUNCTION.
